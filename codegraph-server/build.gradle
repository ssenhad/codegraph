buildscript {
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/release" }
    }
    dependencies {
        classpath libraries.spring.bootGradle
    }
}

plugins {
    id "com.moowork.grunt" version "1.1.1"
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply from: "$rootDir/gradle/distribution.gradle"

bootRun {
    systemProperties = System.properties
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.spring.io/release" }
}

sourceSets {
    acceptance
    if (System.properties.'idea.active') {
        main {
            resources.srcDir file('src/acceptance/resource')
        }
    }
}

dependencies {
    acceptanceCompile sourceSets.main.output
    acceptanceCompile sourceSets.test.output
    acceptanceCompile configurations.testCompile
    acceptanceRuntime configurations.testRuntime

    compile project(':codegraph-core')
    compile project(':codegraph-db')
    compile libraries.spring.boot
    compile libraries.spring.dataNeo4j
    //exclude module: "spring-boot-starter-tomcat"

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testCompile group: 'org.neo4j', name: 'neo4j-kernel', version: versions.neo4jKernel, classifier: 'tests'
}

task acceptanceTests(type: Test) {
    testClassesDir = project.sourceSets.acceptance.output.classesDir
    classpath = project.sourceSets.acceptance.runtimeClasspath
}

node {
    yarnWorkDir = file("$projectDir/src/main/resources/public")
    nodeModulesDir = file("$projectDir/src/main/resources/public")
}
grunt {
    workDir = file("$projectDir/src/main/resources/public")
}

grunt_build.dependsOn yarn_install
build.dependsOn grunt_build
bootRepackage.mustRunAfter grunt_build
groupFiles.dependsOn bootRepackage
build.dependsOn acceptanceTests
acceptanceTests.mustRunAfter test
