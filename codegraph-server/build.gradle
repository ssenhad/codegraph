buildscript {
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/release" }
    }
    dependencies {
        classpath libraries.spring.bootGradle
    }
}

plugins {
    id "com.moowork.grunt" version "1.1.1"
    id 'org.unbroken-dome.test-sets' version '1.4.0'
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply from: "$rootDir/gradle/distribution.gradle"

testSets {
    componentTest {
        dirName = 'component-test'
    }
}

bootRun {
    systemProperties = System.properties
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.spring.io/release" }
}

sourceSets {
    acceptance
}

dependencies {
    acceptanceCompile sourceSets.main.output
    acceptanceCompile sourceSets.test.output
    acceptanceCompile configurations.testCompile
    acceptanceRuntime configurations.testRuntime

    compile project(':codegraph-core')
    compile project(':codegraph-db')
    compile libraries.spring.boot

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testCompile 'org.neo4j:neo4j-ogm-test:2.1.3'
    testCompile 'org.neo4j:neo4j-ogm-embedded-driver:2.1.3'
}

task acceptanceTests(type: Test) {
    testClassesDir = project.sourceSets.acceptance.output.classesDir
    classpath = project.sourceSets.acceptance.runtimeClasspath
}

node {
    yarnWorkDir = file("$projectDir/src/main/resources/public")
    nodeModulesDir = file("$projectDir/src/main/resources/public")
}

grunt {
    workDir = file("$projectDir/src/main/resources/public")
}

grunt_build.dependsOn yarn_install
build.dependsOn grunt_build
bootRepackage.mustRunAfter grunt_build
groupFiles.dependsOn bootRepackage
build.dependsOn acceptanceTests
acceptanceTests.mustRunAfter test
