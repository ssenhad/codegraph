buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath group: 'com.jfrog.bintray.gradle', name: 'gradle-bintray-plugin', version: '1.7'
        classpath group: 'gradle.plugin.nl.javadude.gradle.plugins', name: 'license-gradle-plugin', version: '0.14.0'
    }
}


apply from: "$rootDir/gradle/distribution.gradle"

subprojects {
    apply from: "$rootDir/gradle/repositories.gradle"
    apply from: "$rootDir/gradle/dependencies.gradle"
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: "com.github.hierynomus.license"

    group = 'com.dnfeitosa.codegraph'
    version = "${distribution.versions.major}.${distribution.versions.get(project.name)}-${distribution.versions.modifier}"

    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    license {
        header = rootProject.file('HEADER')
        excludes([
            '**/*.json',
            '**/*.properties',
            '**/*.xml',
            '**/public/node_modules/**/*.*',
            '**/public/yarn-latest/**/*.*',
            '**/public/components/**/*.*',

            '**/*Test.java',
            '**/*TestConfiguration.java',
            '**/*TestBase.java',
            '**/*test.js',

            '**/Gruntfile.js',
            '**/karma.conf.js',
        ])
    }

    dependencies {
        compile group: "commons-lang", name: "commons-lang", version: "2.6"

        testCompile libraries.unitTesting
    }
}


task wrapper(type: Wrapper) {
  gradleVersion = '2.2.1'
}

defaultTasks 'clean', 'build'
